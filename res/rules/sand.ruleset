Define air, sand, stone, and water elements.

air {
	tags: passthrough
	color: #9bd7e8
	key: a
}

sand {
	tags: solid, fallable, pileable
	color: #f2d994
	variance: 5% 5% 10%
	key: s
}

stone {
	tags: solid
	color: #808080
	variance: 2% 1% 10%
	key: x
}

water {
	tags: fallable, pileable, liquid
	color: #0080ff
	variance: 10% 10% 5%
	key: w
}


Fallables fall:
rule (f: fallable, 0: passthrough) {
	f
	0
	=>
	0
	f
}

Pileables slide down slopes:
rule x (p: pileable, s: solid, 0: passthrough) {
	p.
	s0
	=>
	0.
	sp
}

Liquids slide on other liquids:
rule x (l: liquid, a: liquid, 0: passthrough) {
	l0
	a.
	=>
	0l
	a.
}

Liquids slide on other solids:
rule x (l: liquid, s: solid, 0: passthrough) {
	l0
	s.
	=>
	0l
	s.
}

Fallables sink in liquid:
rule 1% (l: liquid, f: fallable) {
	f
	l
	=>
	l
	f
}
